plugins {
    id 'maven-publish'
}
apply from: "../config/library.gradle"

android {
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    // 用于测试
    // json文件文件会导出在工程目录下的schemas文件夹下面。里面会有各个版本数据库的信息
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:${gradle.androidxAppcompat}"

    implementation "androidx.room:room-runtime:${gradle.androidxRoom}"
    annotationProcessor "androidx.room:room-compiler:${gradle.androidxRoom}"

    testImplementation "junit:junit:${gradle.androidxJunit}"
    androidTestImplementation "androidx.test.ext:junit:${gradle.androidxTestJunit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${gradle.androidxTestEspresso}"
}

def isPublishingRelease = gradle.mavePublishingRelease
def mavenVersion = '1.0'
def destinationOutputDir = 'build/outputs/aar'
project.version = "${mavenVersion}-${isPublishingRelease ? 'Release' : 'SNAPSHOT'}"

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    destinationDirectory = file(destinationOutputDir)
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "${gradle.maveGroupId}"
            artifactId "${project.name}"
            version "${project.version}"

            // build/outputs/aar/Font-debug.aar
            artifact "${destinationOutputDir}/${project.name}-${isPublishingRelease ? 'release' : 'debug'}.aar"
            artifact sourcesJar

            // The publication doesn't know about our dependencies,
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group != null && it.name != null && "unspecified" != it.name && it.version != null) {
                        addPomDependencies(it, dependenciesNode)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // 配置仓库地址 Configure warehouse address
            if (isPublishingRelease) {
                url = "${gradle.maveUrlRelease}"
//                credentials {
//                    username = System.getenv('OSS_USER')
//                    password = System.getenv('OSS_PASSWORD')
//                }
            } else {
                url = "${gradle.maveUrlSnapshot}"
            }
        }
    }
}
