plugins {
    id 'maven-publish'
}
apply from: "../config/library.gradle"

dependencies {
    implementation project(path: ':Utils')

    implementation "androidx.appcompat:appcompat:${gradle.androidxAppcompat}"
    implementation "androidx.recyclerview:recyclerview:${gradle.androidxRecyclerView}"
    implementation "androidx.viewpager2:viewpager2:${gradle.androidxViewPager2}"
    implementation "androidx.constraintlayout:constraintlayout:${gradle.androidxConstraintlayout}"

    // AppBarLayout不属于androidx下，已经移动到google/material包下了
    implementation "com.google.android.material:material:${gradle.googleMaterial}"

    testImplementation "junit:junit:${gradle.androidxJunit}"
    androidTestImplementation "androidx.test.ext:junit:${gradle.androidxTestJunit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${gradle.androidxTestEspresso}"
}

def isRelease = false
def mavenVersion = '1.0'
def destinationOutputDir = 'build/outputs/aar'
project.version = "${mavenVersion}-${isRelease ? 'Release' : 'SNAPSHOT'}"

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    destinationDirectory = file(destinationOutputDir)
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "${gradle.maveGroupId}"
            artifactId "${project.name}"
            version "${project.version}"

            // build/outputs/aar/Font-debug.aar
            artifact "${destinationOutputDir}/${project.name}-${isRelease ? 'release' : 'debug'}.aar"
            artifact sourcesJar

            // The publication doesn't know about our dependencies,
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group != null && it.name != null && "unspecified" != it.name && it.version != null) {
                        addPomDependencies(it, dependenciesNode)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // 配置仓库地址 Configure warehouse address
            if (isRelease) {
                url = "${gradle.maveUrlRelease}"
//                credentials {
//                    username = System.getenv('OSS_USER')
//                    password = System.getenv('OSS_PASSWORD')
//                }
            } else {
                url = "${gradle.maveUrlSnapshot}"
            }
        }
    }
}
